##
## Rules
##
## This sample program will return personalized content based on the user's
## networth set in the My Account - Custom Fields section of the Control Panel.
##
## For example, suppose the current user has a net worth of 150,000 and is a
## member of the Liferay community. All assets within the Liferay community
## that are tagged with "affluent" will be returned.
##

package com.liferay.sampledrools.dependencies;

import com.liferay.portal.kernel.search.BooleanClause;
import com.liferay.portal.kernel.search.BooleanClauseOccur;
import com.liferay.portal.kernel.search.BooleanClauseFactoryUtil;
import com.liferay.portal.kernel.search.BooleanQuery;
import com.liferay.portal.kernel.search.BooleanQueryFactoryUtil;
import com.liferay.portal.kernel.search.Document;
import com.liferay.portal.kernel.search.FacetedSearcher;
import com.liferay.portal.kernel.search.Field;
import com.liferay.portal.kernel.search.Hits;
import com.liferay.portal.kernel.search.Indexer;
import com.liferay.portal.kernel.search.SearchContext;
import com.liferay.portal.kernel.search.facet.AssetEntriesFacet;
import com.liferay.portal.kernel.search.facet.Facet;
import com.liferay.portal.kernel.search.facet.ScopeFacet;
import com.liferay.portal.kernel.util.GetterUtil;
import com.liferay.portal.kernel.util.KeyValuePair;
import com.liferay.portal.kernel.util.ObjectValuePair;
import com.liferay.portal.kernel.util.StringUtil;
import com.liferay.portal.kernel.util.Validator;
import com.liferay.portal.model.Group;
import com.liferay.portal.model.User;
import com.liferay.portal.security.auth.PrincipalThreadLocal;
import com.liferay.portal.security.permission.PermissionChecker;
import com.liferay.portal.security.permission.PermissionCheckerFactoryUtil;
import com.liferay.portal.security.permission.PermissionThreadLocal;
import com.liferay.portal.util.PortalUtil;
import com.liferay.portlet.asset.model.AssetEntry;
import com.liferay.portlet.asset.service.AssetEntryLocalServiceUtil;
import com.liferay.portlet.expando.model.ExpandoBridge;

import java.io.Serializable;

import java.util.ArrayList;
import java.util.Collections;
import java.util.HashMap;
import java.util.Iterator;
import java.util.List;
import java.util.Map;

dialect "java"

function List getAssetEntries(
	User user, long[] classNameIds, String assetTagName) {

	List<Group> groups = user.getMySiteGroups();

	long[] groupIds = new long[groups.size()];

	for (int i = 0; i < groups.size(); i++) {
		Group group = groups.get(i);

		groupIds[i] = group.getGroupId();
	}

	if ((classNameIds == null) || (classNameIds.length == 0) ||
		(groupIds.length == 0) || Validator.isNull(assetTagName)) {

		return Collections.emptyList();
	}

	SearchContext searchContext = new SearchContext();

	Facet assetEntriesFacet = new AssetEntriesFacet(searchContext);

	assetEntriesFacet.setStatic(true);

	searchContext.addFacet(assetEntriesFacet);

	Facet scopeFacet = new ScopeFacet(searchContext);

	scopeFacet.setStatic(true);

	searchContext.addFacet(scopeFacet);

	searchContext.setAttribute("paginationType", "regular");
	searchContext.setCompanyId(user.getCompanyId());
	searchContext.setGroupIds(groupIds);
	searchContext.setIncludeStagingGroups(false);
	searchContext.setScopeStrict(false);
	searchContext.setUserId(user.getUserId());

	BooleanQuery booleanQuery = BooleanQueryFactoryUtil.create(searchContext);

	booleanQuery.addExactTerm(Field.ASSET_TAG_NAMES, assetTagName);

	BooleanClause booleanClause = BooleanClauseFactoryUtil.create(
		searchContext, booleanQuery, BooleanClauseOccur.MUST.getName());

	searchContext.setBooleanClauses(new BooleanClause[] {booleanClause});

	List<AssetEntry> assetEntries = new ArrayList<AssetEntry>();

	Indexer indexer = FacetedSearcher.getInstance();

	Hits hits = indexer.search(searchContext);

	for (int i = 0; i < hits.getDocs().length; i++) {
		Document document = hits.doc(i);

		String entryClassName = document.get(Field.ENTRY_CLASS_NAME);

		long entryClassPK = GetterUtil.getLong(
			document.get(Field.ENTRY_CLASS_PK));
		long rootEntryClassPK = GetterUtil.getLong(
			document.get(Field.ROOT_ENTRY_CLASS_PK));

		if (rootEntryClassPK > 0) {
			entryClassPK = rootEntryClassPK;
		}

		AssetEntry assetEntry = AssetEntryLocalServiceUtil.fetchEntry(
			entryClassName, entryClassPK);

		if (assetEntry != null) {
			assetEntries.add(assetEntry);
		}
	}

	return assetEntries;
}

function Map getUserCustomAttributes(
	User user, String[] userCustomAttributeNames) {

	String contextName = PrincipalThreadLocal.getName();

	PermissionChecker contextPermissionChecker =
		PermissionThreadLocal.getPermissionChecker();

	PrincipalThreadLocal.setName(user.getUserId());

	PermissionChecker permissionChecker =
		PermissionCheckerFactoryUtil.create(user, true);

	PermissionThreadLocal.setPermissionChecker(permissionChecker);

	try {
		ExpandoBridge expandoBridge = user.getExpandoBridge();

		Map<String, Serializable> userCustomAttributes =
			new HashMap<String, Serializable> ();

		for (String userCustomAttributeName : userCustomAttributeNames) {
			Serializable userCustomAttributeValue = expandoBridge.getAttribute(
				userCustomAttributeName);

			userCustomAttributes.put(
				userCustomAttributeName, userCustomAttributeValue);
		}

		return userCustomAttributes;
	}
	finally {
		PrincipalThreadLocal.setName(contextName);

		PermissionThreadLocal.setPermissionChecker(contextPermissionChecker);
	}
}

rule "Initialize Rules"
	salience 1000
	when
		user : User();
		userCustomAttributeNames :
			KeyValuePair(key == "userCustomAttributeNames");
	then
		Map<String, Serializable> userCustomAttributes =
			getUserCustomAttributes(
				user, StringUtil.split(userCustomAttributeNames.getValue()));

		Iterator<Map.Entry<String, Serializable>> itr =
			userCustomAttributes.entrySet().iterator();

		while (itr.hasNext()) {
			Map.Entry<String, Serializable> entry = itr.next();

			String key = entry.getKey();
			Serializable value = entry.getValue();

			insertLogical(new ObjectValuePair(key, value));
		}
end

rule "Get Affluent Content"
	when
		classNameIds : KeyValuePair(key == "classNameIds");
		results : List();
		user : User();
		userCustomAttribute : ObjectValuePair(
			(key == "networth") && (value >= 100000));
	then
		List assetEntries = getAssetEntries(
			user, StringUtil.split(classNameIds.getValue(), 0L), "affluent");

		assetEntries.removeAll(results);

		modify(results) {
			addAll(assetEntries);
		};

		retract(userCustomAttribute);
end

rule "Get Regular Content"
	when
		classNameIds : KeyValuePair(key == "classNameIds");
		results : List();
		user : User();
		userCustomAttribute : ObjectValuePair(
			(key == "networth") && (value < 100000));
	then
		List assetEntries = getAssetEntries(
			user, StringUtil.split(classNameIds.getValue(), 0L), "regular");

		assetEntries.removeAll(results);

		modify(results) {
			addAll(assetEntries);
		};

		retract(userCustomAttribute);
end